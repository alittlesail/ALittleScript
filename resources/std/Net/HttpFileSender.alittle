
namespace ALittle;

// 连接对象
public class IHttpFileSenderNative
{
    public fun GetID() : int {}             // 获取全局ID
    // 设置url
    // file_path是本地文件路径
    // download 为true表示下载，false表示上传
    // start_size 文件的起始位置
    public fun SetURL(string url, string file_path, bool download, int start_size) {}
    public fun Start() {}                   // 开始请求
    public fun Stop() {}                    // 结束请求
    public fun GetPath() : string {}        // 获取文件路径
    public fun GetCurrentSize() : int {}    // 获取已上传的文件大小
    public fun GetTotalSize() : int {}      // 获取文件总大小
}

// Http文件协议接口类，负责处理
public class IHttpFileSender
{
    // 提供子类继承
    protected await fun SendDownloadRPC(string method, any content) : string {}
    protected await fun SendUploadRPC(string method, any content) : string, any {}

    // 提供框架调用
    protected await static InvokeDownload(string method, IHttpFileSender client, any content) : string { return client.SendDownloadRPC(method, content); }
    protected await static InvokeUpload(string method, IHttpFileSender client, any content) : string, any { return client.SendUploadRPC(method, content); }
}

// 短连接文件所有的对象集合，提供客户端使用
private Map<int, IHttpFileSender> __HttpFileSenderMap = new Map<int, IHttpFileSender>();
public static FindHttpFileSender(int id) : IHttpFileSender { return __HttpFileSenderMap[id]; }

public class HttpFileSenderTemplate<NATIVE : IHttpFileSenderNative> : public IHttpFileSender
{
    private NATIVE _interface;
    private lua.thread _co;
    private string _ip;
    private int _port;
    private string _file_path;
    private int _start_size;
    private Functor<(IHttpFileSenderNative)> _callback;        // 下载或者上传的进度回调

    public Ctor(string ip, int port, string file_path, int start_size, Functor<(IHttpFileSenderNative)> callback)
    {
        this._interface = new NATIVE();
        this._ip = ip;
        this._port = port;
        this._file_path = file_path;
        this._start_size = start_size;
        this._callback = callback;
    }

    public await fun SendDownloadRPC(string method, any content) : string
    {
        auto co = lua.coroutine.running();
        if (co == null)
            return "当前不是协程";

        this._co = co;
        __HttpFileSenderMap[this._interface.GetID()] = this;

        if (this._start_size == null)
            this._start_size = 0;

        // 设置URL
        string url = "http://"..this._ip..":"..this._port.."/"..method;
        this._interface.SetURL(String_UrlAppendParamMap(url, content), this._file_path, true, this._start_size);
        this._interface.Start();

        // 协程挂起，并等待返回值
        return yield;
    }

    public await fun SendUploadRPC(string method, any content) : string, any
    {
        auto co = lua.coroutine.running();
        if (co == null)
            return "当前不是协程", null;

        this._co = co;
        __HttpFileSenderMap[this._interface.GetID()] = this;

        if (this._start_size == null)
            this._start_size = 0;

        // 设置URL
        string url = "http://"..this._ip..":"..this._port.."/"..method;
        this._interface.SetURL(String_UrlAppendParamMap(url, content), this._file_path, false, this._start_size);
        this._interface.Start();

        // 协程挂起，并等待返回值
        return yield;
    }

    public fun Stop()
    {
        this._interface.Stop();
    }

    public fun GetTotalSize() : int
    {
        return this._interface.GetTotalSize();
    }

    public fun HandleSucceed()
    {
        __HttpFileSenderMap[this._interface.GetID()] = null;
        auto result, auto reason = lua.coroutine.resume(this._co, null, null);
        if (result != true) Error(reason);
    }

    public fun HandleFailed(string reason)
    {
        __HttpFileSenderMap[this._interface.GetID()] = null;
        auto result, auto reason = lua.coroutine.resume(this._co, reason, null);
        if (result != true) Error(reason);
    }

    public fun HandleProcess()
    {
        if (this._callback != null)
            this._callback(this._interface);
    }
}
