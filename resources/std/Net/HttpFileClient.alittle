
namespace ALittle;

// Http文件协议接口类，负责处理
public class IHttpFileClient
{
    // 提供子类继承
    protected await fun SendDownloadRPC(string method, any content) : string {}
    protected await fun SendUploadRPC(string method, any content) : string, any {}

    // 提供框架调用
    protected await static InvokeDownload(string method, IHttpFileClient client, any content) : string { return client.SendDownloadRPC(method, content); }
    protected await static InvokeUpload(string method, IHttpFileClient client, any content) : string, any { return client.SendUploadRPC(method, content); }
}

// 连接对象
public class IHttpFileInterface
{
    public fun GetID() : int {}
    public fun GetPath() : string {}
    public fun SetURL(string url, string file_path, bool download, int start_size) {}
    public fun Start() {}
    public fun Stop() {}
    public fun GetCurrentSize() : int {}
    public fun GetTotalSize() : int {}
}

// 短连接文件所有的对象集合
private Map<int, IHttpFileClient> __HttpFileClientMap = new Map<int, IHttpFileClient>();
public static FindHttpFileClient(int id) : IHttpFileClient { return __HttpFileClientMap[id]; }

private Map<string, Functor<(IHttpFileClient, any):any>> __all_callback = new Map<string, Functor<(IHttpFileClient, any):any>>();
table.setweak(__all_callback, false, true);
protected static RegHttpFileCallback(string method, Functor<(IHttpFileClient, any):any> callback)
{
    if (__all_callback[method] != null)
    {
        Error("RegHttpCallback消息回调函数注册失败，名字为"..method.."已存在");
        return;
    }
    __all_callback[method] = callback;
}
// 获取回调函数
public static FindHttpFileCallback(string method) : Functor<(IHttpFileClient, any):any>
{
    return __all_callback[method];
}

public class HttpFileClient<HFC : IHttpFileInterface> : public IHttpFileClient
{
    private HFC _interface;
    private thread _co;
    private string _ip;
    private int _port;
    private string _file_path;
    private int _start_size;
    private Functor<(IHttpFileInterface)> _callback;        // 下载或者上传的进度回调

    public Ctor(string ip, int port, string file_path, int start_size, Functor<(IHttpFileInterface)> callback)
    {
        this._interface = new HFC();
        this._ip = ip;
        this._port = port;
        this._file_path = file_path;
        this._start_size = start_size;
        this._callback = callback;
    }

    public await fun SendDownloadRPC(string method, any content) : string
    {
        thread co = coroutine.running();
        if (co == null)
            return "当前不是协程";

        this._co = co;
        __HttpFileClientMap[this._interface.GetID()] = this;

        if (this._start_size == null)
            this._start_size = 0;

        // 设置URL
        string url = "http://"..this._ip..":"..this._port.."/"..method;
        this._interface.SetURL(String_UrlAppendParamMap(url, content), this._file_path, true, this._start_size);
        this._interface.Start();

        // 协程挂起，并等待返回值
        return yield;
    }

    public await fun SendUploadRPC(string method, any content) : string, any
    {
        thread co = coroutine.running();
        if (co == null)
            return "当前不是协程", null;

        this._co = co;
        __HttpFileClientMap[this._interface.GetID()] = this;

        if (this._start_size == null)
            this._start_size = 0;

        // 设置URL
        string url = "http://"..this._ip..":"..this._port.."/"..method;
        this._interface.SetURL(String_UrlAppendParamMap(url, content), this._file_path, false, this._start_size);
        this._interface.Start();

        // 协程挂起，并等待返回值
        return yield;
    }

    public fun Stop()
    {
        this._interface.Stop();
    }

    public fun GetTotalSize() : int
    {
        return this._interface.GetTotalSize();
    }

    public fun HandleSucceed()
    {
        __HttpFileClientMap[this._interface.GetID()] = null;
        assert(coroutine.resume(this._co, null, null));
    }

    public fun HandleFailed(string reason)
    {
        __HttpFileClientMap[this._interface.GetID()] = null;
        assert(coroutine.resume(this._co, reason, null));
    }

    public fun HandleProcess()
    {
        if (this._callback != null)
            this._callback(this._interface);
    }
}

