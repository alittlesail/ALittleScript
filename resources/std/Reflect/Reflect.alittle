
namespace ALittle;

// 协议结构
public struct ReflectInfo
{
    string name;                 // 带命名域的结构体名
    string ns_name;              // 所在命名域名
    string rl_name;              // 实际名
    List<string> name_list;      // 变量名列表
    List<string> type_list;      // 变量类型列表
}

private auto __all_name_reflect = new Map<string, ReflectInfo>();
private auto __all_id_reflect = new Map<int, ReflectInfo>();
protected static RegReflect(int hash, string name, ReflectInfo info)
{
    if (__all_name_reflect[name] != null)
        return;
    ReflectInfo old_info = __all_id_reflect[hash];
    if (old_info != null)
    {
        Error("RegReflect 名字为"..name.."和名字为"..old_info.name.."哈希值冲突");
        return;
    }
    __all_name_reflect[name] = info;
    __all_id_reflect[hash] = info;
}

protected static FindReflectByName(string name) : ReflectInfo
{
    return __all_name_reflect[name];
}

protected static FindReflectById(int id) : ReflectInfo
{
    return __all_id_reflect[id];
}
